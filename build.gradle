import org.jetbrains.kotlin.gradle.dsl.ExplicitApiMode

buildscript {

    ext.compileJavaVersion = "11"

    //https://github.com/Kotlin/kotlinx.coroutines
    ext.coroutinesVersion = "1.6.4"
    //https://github.com/csense-oss/csense-kotlin-test
    ext.csenseTestsVersion = "0.0.59"
    //https://github.com/csense-oss/csense-kotlin-annotations
    ext.csenseAnnotationVersion = "0.0.60"

    //https://github.com/JetBrains/kotlin/releases
    ext.kotlin_version = "1.8.10"

    //https://github.com/Kotlin/kotlinx-benchmark
    ext.kotlinx_benchmark_version = "0.4.7"

    //library settings - https://github.com/csense-oss/csense-kotlin
    ext.csenseVersionName = "0.1.0-snapshot-1"

    ext.csenseUserOrg = "csense-oss"
    ext.csenseGroupId = "csense.kotlin"
    ext.csenseArtifactId = "csense-kotlin"
    ext.csenseLicense = "MIT"
    ext.csenseLicenseUrl = "https://mit-license.org/"

    ext.csenseDeveloperAlias = "tvede-dk"
    ext.csenseDeveloperName = "Kasper Tvede"
    ext.csenseDeveloperEmail = "csenseoss@tvedesys.dk"

    ext.csenseWebsite = "https://github.com/csense-oss/csense-kotlin"
    ext.csenseGit = "https://github.com/csense-oss/csense-kotlin.git"
    ext.csenseArtifactDescription = "aim of this project is to add further extensions, features to the std lib and \"very common\" operations."

}


plugins {
    id "maven-publish"
    //https://github.com/JetBrains/kotlin
    id 'org.jetbrains.kotlin.multiplatform' version "$kotlin_version"
    //https://github.com/Kotlin/kotlinx-benchmark
    id 'org.jetbrains.kotlinx.benchmark' version "$kotlinx_benchmark_version"
    //https://github.com/jeremylong/DependencyCheck https://plugins.gradle.org/plugin/org.owasp.dependencycheck
    id "org.owasp.dependencycheck" version "8.1.0"
}

repositories {
    mavenCentral()
    maven {
        url 'https://pkgs.dev.azure.com/csense-oss/csense-oss/_packaging/csense-oss/maven/v1'
        name 'csense-oss'
    }
}

kotlin {
    setExplicitApi(ExplicitApiMode.Strict)

    iosX64 {

    }
    macosX64 {

    }

    jvm {
        testRuns["test"].executionTask.configure {
            useJUnitPlatform()
        }

        jvmToolchain {
            languageVersion.set(JavaLanguageVersion.of(compileJavaVersion))
        }
    }

    js(IR) { //https://kotlinlang.org/docs/js-ir-compiler.html
        browser {
            testTask {
                useKarma {
                    //useFirefox()
                    useChrome()
                }
            }
        }
        nodejs()
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
                implementation "csense.kotlin:csense-kotlin-annotations:$csenseAnnotationVersion"
            }
        }
        commonTest {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"
                implementation "csense.kotlin:csense-kotlin-tests:$csenseTestsVersion"
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {

        }

        jvmTest {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-benchmark-runtime:$kotlinx_benchmark_version"
            }
        }

        jsMain {

        }

        jsTest {

        }

        iosX64 {

        }
        macosX64 {

        }

        configureEach {
            languageSettings {
                languageVersion = "1.8"
                apiVersion = "1.8"
                optIn("kotlin.contracts.ExperimentalContracts")
                optIn("kotlin.experimental.ExperimentalTypeInference")
                optIn("kotlinx.coroutines.ExperimentalCoroutinesApi")
                progressiveMode = true //https://kotlinlang.org/docs/whatsnew13.html#progressive-mode
            }
        }
    }
    targets.configureEach {
        compilations.configureEach {
            kotlinOptions {
                allWarningsAsErrors = true
            }
        }
    }
}

dependencyCheck {
    def allConfigsAsList = new ArrayList<>(configurations.getNames())
    def nonTestConfigurations = allConfigsAsList.findAll {
        !it.contains("Test") && (
                it.startsWith("js") ||
                        it.startsWith("jvm") ||
                        it.startsWith("jvm") ||
                        it.startsWith("common")
        )
    }

    analyzers {
        assemblyEnabled = false
    }
    scanConfigurations = nonTestConfigurations
    skipTestGroups = true
    failOnError = true
}

project.group = csenseGroupId
project.version = csenseVersionName

apply from: "$rootDir/gradle/publish.gradle"

task uploadProd(dependsOn: ["assemble", "allTests", "dependencyCheckAggregate", "publish"])

benchmark {
    targets {
//        register("jvmTest") {
//            see https://mvnrepository.com/artifact/org.openjdk.jmh/jmh-core / https://github.com/openjdk/jmh
//            jmhVersion = "1.36" // available only for JVM compilations & Java source sets
//        }
    }
}

